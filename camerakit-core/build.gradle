apply plugin: 'com.android.library'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionName rootProject.ext.versionName
    }
    lintOptions {
        abortOnError false
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/base'
        main.java.srcDirs += 'src/main/api16'
        main.java.srcDirs += 'src/main/api21'
        main.java.srcDirs += 'src/main/types'
        main.java.srcDirs += 'src/main/events'
        main.java.srcDirs += 'src/main/utils'
        main.java.srcDirs += 'src/main/vision'
    }
    externalNativeBuild {
        cmake {
            path 'src/main/CMakeLists.txt'
        }
    }
}
task clearJar(type: Delete) {
    delete 'build/hi.jar' //jar包的名字，随便命名
}
task makeJar(type:org.gradle.api.tasks.bundling.Jar) {
    //指定生成的jar名
    baseName 'hi'
    //从哪里打包class文件
    from('build/intermediates/bundles/default/')
    //打包到jar后的目录结构
    into('build/')
    //去掉不需要打包的目录和文件
    exclude('test/', 'BuildConfig.class', 'R.class')
    //去掉R开头的文件
    exclude{it.name.startsWith('R');}
}
makeJar.dependsOn(clearJar, build)
dependencies {
    compile 'com.android.support:appcompat-v7:27.0.2'
    compile 'com.android.support:exifinterface:27.0.2'
    implementation 'com.google.android.gms:play-services-vision:11.6.2'
}

group = 'com.wonderkiln'
version = rootProject.ext.versionName

install {
    repositories.mavenInstaller {
        pom.project {
            name 'CameraKit-Core-Android'
            packaging 'aar'
        }
    }
}

bintray {
    user = rootProject.ext.bintrayUser
    key = rootProject.ext.bintrayKey
    override = true
    publish = true
    configurations = ['archives']
    pkg {
        repo = 'snapshots'
        name = 'CameraKit-Core-Android'
        userOrg = 'wonderkiln'
        vcsUrl = 'https://github.com/wonderkiln/CameraKit-Android.git'
        version {
            name = rootProject.ext.versionName
            vcsTag = rootProject.ext.versionName
            released = new Date()
        }
    }
}
